plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

ext {
    versionName = "0.1"
}

android {
    namespace 'io.github.toyota32k.utils'
    compileSdk 33

    defaultConfig {
        minSdk 26
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = 'https://maven.pkg.github.com/toyota-m2k/android-utilities'

            credentials {
                def localProperties = new Properties()
                localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

                username = localProperties.containsKey("com.github.user") ? localProperties.getProperty('com.github.user') : ''
                password = localProperties.containsKey("com.github.token") ? localProperties.getProperty('com.github.token') : ''
            }
        }
    }

    publications {
        GitHubPackages(MavenPublication) {
            groupId "com.github.toyota-m2k"
            artifactId "android-utilities"
            version versionName

            afterEvaluate {
                from components.release
            }

            pom {
                url = 'https://github.com/toyota-m2k/android-utilities'
                licenses {
                    license {
                        name = 'Apache License Version 2.0'
                    }
                }
            }
        }
    }
}